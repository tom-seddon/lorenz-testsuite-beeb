skipped_test: .macro text,next_name
                .init_beeb_hacks

                ; disable paged mode, in case the last test enabled
                ; it.
                lda #15
                jsr $ffee
                lda #0
                sta paged
                
                ldx #0
loop:
                lda text,x
                beq done
                jsr print_char
                inx
                bne loop
done:
                lda #<name
                sta $bb
                lda #>name
                sta $bc
                jmp load_file
                
text: .text 13,\text,0
name: .text \next_name,0
                .endm
                
init_beeb_hacks: .macro
                jsr init_beeb_hacks_routine
                ldx #$ff
                txs
                lda #$7f
                pha
                lda #$ff
                pha
                .endm

init_beeb_hacks_routine:
                lda $202
                sta old_brkv+0
                lda $203
                sta old_brkv+1
                
                lda #<c64_style_brk_handler
                sta $202
                lda #>c64_style_brk_handler
                sta $203

                lda #<default_brk_routine
                sta $316
                lda #>default_brk_routine
                sta $317

                lda #0
                sta 2

                ldx #0

save_pages_loop:
                lda $1000,x
                sta buffer+$000,x
                lda $1100,x
                sta buffer+$100,x
                lda $1200,x
                sta buffer+$200,x
                lda $1300,x
                sta buffer+$300,x
                inx
                bne save_pages_loop

                ; shaiy uses this.
                lda $1aff
                sta old_1aff
                
                rts

uninit_beeb_hacks:
                lda old_brkv+0
                sta $202
                lda old_brkv+1
                sta $203

                ldx #0
restore_pages_loop:
                lda buffer+$000,x
                sta $1000,x
                lda buffer+$100,x
                sta $1100,x
                lda buffer+$200,x
                sta $1200,x
                lda buffer+$300,x
                sta $1300,x
                inx
                bne restore_pages_loop

                lda old_1aff
                sta $1aff

                rts                

default_brk_routine:
                pla
                tay
                pla
                tax
                pla
                jmp (old_brkv)

c64_style_brk_handler:
                pha
                txa
                pha
                tya
                pha
                tsx
                jmp ($0316)
                
print_char: .proc
                cmp #$80
                bcs done
                cmp #32
                bcs print
                cmp #13
                beq print
                cmp #10
                bne done
print:
                jsr $ffe3
done:
                rts
                .endproc
                
scan_keyboard:
                ; don't bother waiting for a key - just enable paged
                ; mode.
                lda paged
                bne paged_mode_set
                lda #14
                jsr $ffee
                lda #1
                sta paged
paged_mode_set:

                ; Check for Escape.
                bit $ff
                bmi exit

                ; Z=0 to keep waiting, A=3 to quit - not much point
                ; doing either on the Beeb, the Escape key is fine.
                lda #1
                
                rts

exit:
                ldx #<star_basic
                ldy #>star_basic
                jmp $fff7

star_basic:
                .text "BASIC",13

load_file:
                jsr uninit_beeb_hacks
                
                ldy #0
copy_name_loop:
                lda ($bb),y
                sta buffer,y
                beq copy_name_done
                iny
                bne copy_name_loop
copy_name_done:
                lda #13
                sta buffer,y
                ldx #<buffer
                ldy #>buffer
                jmp $fff7


d011_hack:
                .byte $80       ;any negative value, so the waitborder
                                ;check just falls right through
                
                .align 2
old_brkv:
                .fill 2
buffer:
                .fill 1024
old_1aff:
                .fill 1
paged:
                .fill 1